module SaleProgressDetails

import {
    REQT,
    bREQT,
    REQTgroup,
    assertREQTgroup,
    bREQTgroup,
    logGroup,
    logGroupUnit,
    logGroupStart,
    logGroupEnd,
    TODO
} from StellarHeliosHelpers

struct SaleProgressDetailsV1 {
    lastPurchaseAt: Time 
    prevPurchaseAt: Time 

    lotCount: Int 
    lotsSold: Int 

    func validateDetailsWhenPending(self, 
        saleStartTime : Time, 
        totalSaleLots : Int
     ) -> Bool {
        logGroup("SaleProgressDetails: validateCreatedDetails()", false, () -> Bool {
            assert(self.lastPurchaseAt == saleStartTime, "lastPurchaseAt must be equal to startAt");
            assert(self.prevPurchaseAt == saleStartTime, "prevPurchaseAt must be equal to startAt");
            assert(self.lotCount == totalSaleLots, "lotCount must be equal to totalSaleLots");
            assert(self.lotsSold == 0, "lotsSold must be zero");
            // assert(self.lastPurchaseAt >= self.prevPurchaseAt, "lastPurchaseAt must be greater than or equal to prevPurchaseAt");
            true    
        })
    }

    func validateUpdatedDetails(
        self, 
        prevProgress: SaleProgressDetailsV1, 
        now: Time,
        lotsSold: Int
    ) -> Bool {
        logGroup("SaleProgressDetails: validateUpdatedDetails()", false, () -> Bool {
            bREQTgroup("Updates last-purchase-time to the current tx time", false, () -> Bool {
                lps = self.lastPurchaseAt.show();
                nowStr = now.show();
                print("   ----  lastPurchaseAt: " + lps);
                print("   ----  now: " + nowStr);
                assert(
                    (self.lastPurchaseAt == now),
                    "lastPurchaseAt not updated correctly"
                )
                print("✔ lastPurchaseAt updated OK")
                true
            })

            && bREQTgroup("Keeps a record of the prior purchase time for dynamic pacing", false, () -> Bool {
                prevNow : Time = prevProgress.lastPurchaseAt;
                assert(self.prevPurchaseAt == prevNow,
                    "must update prevPurchaseAt to prev lastPurchaseAt " + prevNow.show() + 
                    ", got " + self.prevPurchaseAt.show()
                )
                print("✔ prevPurchaseAt updated OK")
                true
            })

            && bREQTgroup("Updates lotsSold to include the new lots being purchased", false, () -> Bool {
                prevLotsSoldStr = prevProgress.lotsSold.show(); /** todo: inline */
                print("   ----  prev lotsSold: " + prevLotsSoldStr);
                lotsSoldStr = self.lotsSold.show(); /** todo: inline */
                print("   ----  lotsSold: " + lotsSoldStr);
                assert(
                    (
                        self.lotsSold == prevProgress.lotsSold + lotsSold
                    ),
                    "lotsSold not updated correctly"
                )
                print("✔ lotsSold updated OK")
                true
            }) 

            && bREQTgroup("Ensures the total number of lots available for purchase is unchanged", false, () -> Bool {
                assert(
                    (self.lotCount == prevProgress.lotCount),
                    "must not change chunk's lot count"
                )
                print("✔ lotCount unchanged OK")
                true
            }) 
            && true
        })
    }
}